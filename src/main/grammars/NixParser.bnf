{
  parserClass="org.nixdea.parser.NixParser"
  parserUtilClass="org.nixdea.parser.NixParserUtil"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  psiClassPrefix="Nix"
  psiImplClassSuffix="Impl"
  psiPackage="org.nixdea.psi"
  psiImplPackage="org.nixdea.psi.impl"

  elementTypeHolderClass="org.nixdea.psi.NixTypes"
  elementTypeClass="org.nixdea.psi.NixElementType"
  tokenTypeClass="org.nixdea.psi.NixTokenType"

  psiImplUtilClass="org.nixdea.parser.NixParserUtil"

  parserImports=[
    "static com.intellij.lang.parser.GeneratedParserUtilBase.*"
  ]

  generate=[visitorValue="T"]
  tokens = [
    space='regexp:\s+'
    comment='regexp:#.*'

    ASSIGN = '='
    LPAREN = '('
    RPAREN = ')'
    LCURLY = '{'
    RCURLY = '}'
    LBRAC = '['
    RBRAC = ']'
    DOLLAR_CURLY = '${'
    IS = '?'
    NAMED = '@'
    COLON = ':'
    SEMI = ';'
    AND = '&&'
    OR = '||'
    NOT = '!'
    EQ = '=='
    NEQ = '!='
    LEQ = '<='
    GEQ = '>='
    LT = '<'
    GT = '>'
    PLUS = '+'
    MINUS = '-'
    DIVIDE = '/'
    TIMES = '*'
    CONCAT = '++'
    DOT = '.'
    COMMA = ','
    IMPL = '->'
    UPDATE = '//'

    ASSERT = 'assert'
    IF = 'if'
    ELSE = 'else'
    THEN = 'then'
    WITH = 'with'
    LET = 'let'
    IN = 'in'
    REC = 'rec'
    OR_KW = 'or'
    ELLIPSIS = '...'
    INHERIT = 'inherit'
    IMPORT = 'import'

    SCOMMENT = 'regexp:#[^\r\n]*'
    MCOMMENT = 'regexp:/\*([^*]|\*[^/])*\*/'

    INT = "regexp:[0-9]+"
    BOOL = "regexp:(true|false)"
    ID = "regexp:[a-zA-Z][a-zA-Z_0-9'-]*"

    PATH = "regexp:[a-zA-Z0-9._\-+]*(/[a-zA-Z0-9._\-+]+)+"
    SPATH = "regexp:<[a-zA-Z0-9._\-+]+(/[a-zA-Z0-9._\-+]+)*>"
    HPATH = "regexp:~(/[a-zA-Z0-9._\-+]+)+"
    URI = "regexp:[a-zA-Z][a-zA-Z0-9+\-.]*:[a-zA-Z0-9%/?:@&=+$,\-_.!~*']+"

    DOUBLE_QUOTE = "\""

    SINGLE_QUOTE_TWICE= "''"
  ]

  extends(".*_op_expr")=op_expr
  implements("lambda|let_expr|rec_set")="org.nixdea.psi.HasDeclarations"
}

nixFile ::= !<<eof>> expr

expr ::= lambda
       | if_expr
       | with_expr
       | assert_expr
       | let_expr
       | op_expr

lambda ::= param COLON expr { extends="expr" pin=2 mixin="org.nixdea.psi.mixins.NixLambdaImplMixin" }
if_expr ::= IF expr THEN expr ELSE expr { extends="expr" pin=1 }
with_expr ::= WITH expr SEMI expr { extends="expr" pin=1 }
assert_expr ::= ASSERT expr SEMI expr { extends="expr" pin=1 }
let_expr ::= LET binds IN expr { extends="expr" pin=1 mixin="org.nixdea.psi.mixins.NixLetExprImplMixin" }

param ::= param_starting_with_set | param_starting_with_id
param_starting_with_set ::= param_set [NAMED param_set_id] { pin=1 }
param_starting_with_id ::= param_set_id [NAMED param_set] { pin=1 }
param_set_id ::= ID { mixin="org.nixdea.psi.mixins.NixNameOwnerMixin" }
param_set ::= LCURLY param_set_attrs ELLIPSIS? RCURLY { pin=1 }
private param_set_attrs ::= param_set_attrs_with_recover*

private param_set_attrs_with_recover ::=
    !(RCURLY | ELLIPSIS | INHERIT | (attr_path ASSIGN) | <<eof>>) param_set_attr { pin=1 recoverWhile=param_recover }

// the part in parens means: it ends with a comma, or, if it doesn't, the next token must be a right curly brace
param_set_attr ::= ID with_default? (COMMA | &RCURLY) { mixin="org.nixdea.psi.mixins.NixNameOwnerMixin" }
private with_default ::= IS expr { pin=1 }
private param_recover ::= !(RCURLY | ELLIPSIS | ID)

op_expr ::= implies_op_expr
          | or_op_expr
          | and_op_expr
          | eq_group
          | comp_group
          | set_union_op_expr
          | bool_negation_op_expr
          | add_group
          | mul_group
          | list_concat_op_expr
          | attr_test_op_expr
          | arith_negation_op_expr
          | call_op_expr
          | import_op_expr
          | select_or_atom
          { extends="expr" }

private mul_group ::= mul_op_expr | div_op_expr
private add_group ::= add_op_expr | sub_op_expr
private comp_group ::= lt_op_expr | gt_op_expr | lte_op_expr | gte_op_expr
private eq_group ::= eq_op_expr | neq_op_expr

select_or_atom ::= select | atom { extends="op_expr" }
select ::= atom select_path [ OR_KW select_or_atom ] { pin=2 extends="select_or_atom" }
private select_path ::= (DOT attr)+ { pin(".*")=1 }

import_op_expr ::= IMPORT op_expr { pin=1 }
call_op_expr ::= op_expr (parens | select_or_atom | atom)
arith_negation_op_expr ::= MINUS op_expr
attr_test_op_expr ::= op_expr IS attr_path { pin=2 }
list_concat_op_expr ::= op_expr CONCAT op_expr { rightAssociative=true }

mul_op_expr ::= op_expr TIMES op_expr { pin=2 }
div_op_expr ::= op_expr DIVIDE op_expr { pin=2 }

add_op_expr ::= op_expr PLUS op_expr { pin=2 }
sub_op_expr ::= op_expr MINUS op_expr { pin=2 }

bool_negation_op_expr ::= NOT op_expr { pin=1 }
set_union_op_expr ::= op_expr UPDATE op_expr { pin=2 rightAssociative=true }

lt_op_expr ::= op_expr LT op_expr { pin=2 }
gt_op_expr ::= op_expr GT op_expr { pin=2 }
lte_op_expr ::= op_expr LEQ op_expr { pin=2 }
gte_op_expr ::= op_expr GEQ op_expr { pin=2 }

eq_op_expr ::= op_expr EQ op_expr { pin=2 }
neq_op_expr ::= op_expr NEQ op_expr { pin=2 }

and_op_expr ::= op_expr AND op_expr { pin=2 }
or_op_expr ::= op_expr OR op_expr { pin=2 }
implies_op_expr ::= op_expr IMPL op_expr { pin=2 }

attr ::= simple_string | ID | antiquotation | OR_KW

atom ::= ref
       | integer
       | boolean
       | list
       | set
       | path_lit
       | string
       | parens
       { extends="op_expr" }

ref ::= ID { mixin="org.nixdea.psi.mixins.NixRefImplMixin" extends="atom" }
integer ::= INT { extends="atom" }
boolean ::= BOOL { extends="atom" }

parens ::= LPAREN expr RPAREN { extends="atom" pin=1 }
path_lit ::= PATH | SPATH | HPATH { extends="atom" }
list ::= LBRAC list_elem* RBRAC { extends="atom" pin=1 }
private list_elem ::= !(RBRAC) select_or_atom { pin=1 /*recoverWhile="list_elem_recover"*/ }
//private list_elem_recover ::= !(RBRAC) // TODO: write proper list recover

set ::= rec_set | simple_set { extends="atom" }
rec_set ::= REC simple_set { pin=1 extends="set" mixin="org.nixdea.psi.mixins.NixRecSetImplMixin" }
simple_set ::= LCURLY binds? RCURLY !COLON { pin=1 extends="set" }

string ::= simple_string | ind_string | uri_string { extends="atom"}
uri_string ::= URI { extends="string" }
simple_string ::= DOUBLE_QUOTE (STR | antiquotation)* DOUBLE_QUOTE { pin=1 extends="string" }
ind_string ::= SINGLE_QUOTE_TWICE (IND_STR | antiquotation)* SINGLE_QUOTE_TWICE { pin=1 extends="string" }
antiquotation ::= DOLLAR_CURLY expr RCURLY { pin=1 }

private binds ::= binding_with_recover+
private binding_with_recover ::= !(IN | RCURLY) binding { pin=1 recoverWhile="binding_recover" }
binding ::= inherit_attrs | attr_assign
inherit_attrs ::= INHERIT (LPAREN expr RPAREN)? attr* SEMI { extends="binding" pin=1 }
attr_assign ::= attr_path ASSIGN expr SEMI { extends="binding" pin=1 mixin="org.nixdea.psi.mixins.NixNameOwnerMixin" }
private binding_recover ::= !((attr_path ASSIGN) | INHERIT | IN | RCURLY)

attr_path ::= attr (DOT attr)*
